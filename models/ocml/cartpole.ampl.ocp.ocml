constant pi := 3.14159265359;

constant g := 9.81;
constant l := 1;
constant cart_mass := 1;
constant pole_mass := 0.1;

constant p0 := 0;
constant theta0 := 0;

independent t := 1, >= 0, <= 20;

state p := p0, >= -5, <= 5;
state theta := theta0, >= -2*pi, <= 2*pi;
state v := 0 >= -20, <= 20;
state omega := 0 >= -2*pi, <= 2*pi;

control F := -1, >= -30 <= 30;

constant phl := 0.5*l;
constant total_mass := cart_mass+pole_mass;
constant com_pole := pole_mass*phl;

function ctheta := cos(theta);
function stheta := sin(theta);
function domega :=  (g*stheta + ctheta * (-F-com_pole*omega^2*stheta) / total_mass) / (phl * (4.0 / 3.0 - pole_mass * ctheta^2 / total_mass));
function dv := (F + com_pole*(omega^2*stheta-domega*ctheta)) / total_mass;

minimize lagrange force(x,z,u,p,t): F^2;
minimize arrival time(x,p,t): t; 

subject to
  dpx: diff(p, t) = v;
  dpy: diff(theta, t) = omega;
  dvx: diff(v, t) = dv;
  dvy: diff(omega, t) = domega;

  ic1: eval(p, 0) = p0;
  ic2: eval(v, 0) = 0;
  ic3: eval(theta, 0) = theta0;
  ic4: eval(omega, 0) = 0;

  fc1: eval(p, tf) = 0;
  fc2: eval(v, tf) = 0;
  fc3: eval(theta, tf) = 0;
  fc4: eval(omega, tf) = 0;

let F.type := "u0";
option solver muscod;
option muscod_options "nshoot=60 atol=1e-6 itmax=200";
option muscod_auxfiles "rc";
solve;
